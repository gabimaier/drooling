package loan.advisor.dslr

import loan.advisor.dto.Person
import loan.advisor.dto.LoanType
import loan.advisor.services.FeedbackService

global FeedbackService feedbackService

expander dictionary.dsl

rule "Underage Persons"
	salience 100
    when
        There is a person younger than 18
    then
    	Tell that person "you have to grow up first!"
    	Exclude that person
end

rule "Above maximum age limit"
    when
    	For an active loan type
        A person is too old
    then
    	Tell that person they do not qualify because of "maximum age limit"
end

rule "Bellow minimum income"
    when
    	For an active loan type
        A person earns too little
    then
    	Tell that person they do not qualify because of "minimum income limit"
end

rule "Invalid citizenship"
    when
    	For an active loan type
    	A person is of incompatible citizenship
    then
        Tell that person they do not qualify because of "incompatible citizenship"
end

rule "Valid for loan"
	when
		For an active loan type
		There is a person that
		- is at least 18 years old
		- is not too old for the loan
		- earns more than or equal to the minimum income for the loan
		- has compatible citizenship with the loan
	then
		Tell that person they qualify for the loan
end