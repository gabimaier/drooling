package loan.advisor.dslr

import loan.advisor.dto.Person
import loan.advisor.dto.LoanType
import loan.advisor.services.FeedbackService

global FeedbackService feedbackService

expander dictionary.dsl

rule "Underage Persons"
	salience 100
    when
        There is a person younger than 18
    then
    	Use feedbackService to tell that person "you have to grow up first!"
    	Exclude that person
end

rule "Above maximum age limit"
    when
    	For an active loan type
        A person is too old
    then
    	Use feedbackService to give this negative feedback "you exceed the maximum age limit"
end

rule "Bellow minimum income"
    when
    	For an active loan type
        A person earns too little
    then
    	Use feedbackService to give this negative feedback "your income is bellow the minimum limit"
end

rule "Invalid citizenship"
    when
    	For an active loan type
    	A person is of incompatible citizenship
    then
        Use feedbackService to give this negative feedback "you do not have a copmatible citizenship"
end

rule "Valid for loan"
	when
		For an active loan type
		There is a person that
		- is at least 18 years old
		- is not too old for the loan
		- earns more than or equal to the minimum income for the loan
		- has compatible citizenship with the loan
	then
		Use feedbackService to give positive feedback
end