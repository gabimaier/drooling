//created on: Jun 8, 2013
package loan.advisor.drl

//list any import classes here.

import loan.advisor.dto.Person
import loan.advisor.dto.LoanType

//declare any global variables here


rule "Underage Persons"
	salience 100			//filter out the kids first
    when
        $person: Person(age < 18)
    then
    	System.out.printf("%S, you have to grow up first!\n", $person.getName());
    	retract($person);
end

rule "Above maximum age limit"
    when
    	LoanType($loanName: name, $maxAge: maxAge)
        Person(age > $maxAge, $persName: name)
    then
        System.out.printf("%S, you do NOT qualify for '%s': you are older than %d.\n", 
        	$persName, $loanName, $maxAge);
end

rule "Bellow minimum income"
    when
    	LoanType($loanName: name, $minIncome: minIncome)
        Person(income < $minIncome, $persName: name)
    then
        System.out.printf("%S, you do NOT qualify for '%s': you earn less than %,.2f â‚¬.\n", 
        	$persName, $loanName, $minIncome);
end

rule "Invalid citizenship"
    when
    	LoanType($loanName: name, $compatibleCitizenships: compatibleCitizenships)
        Person(!$compatibleCitizenships.contains(citizenship), $persName: name)
    then
        System.out.printf("%S, you do NOT qualify for '%s': this loan is available only in %s .\n", 
        	$persName, $loanName, $compatibleCitizenships.toString());
end

rule "Valid for loan"
	when
		$loan: LoanType()
		Person(age >= 18, age <= $loan.getMaxAge(), 
			income >= $loan.getMinIncome(),
			$loan.getCompatibleCitizenships().contains(citizenship),
			$name: name	)
	then
		System.out.printf("%S, you qualify for '%s'! One of our agents will contact you shortly.\n",
			$name, $loan.getName());
end
